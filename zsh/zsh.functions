#!/usr/bin/env zsh -eu

function getuuid {
  ruby -r securerandom -e 'print SecureRandom.uuid'
}

gettty(){ echo "$TTY" | sed -e 's/[^0-9]//g' }

relative(){
  echo ${1:a} | sed -e "s|^$PWD/\?||"
}

export AUTOLS_DIR=$PWD
autols() {
  [[ -n $AUTOLS_DIR && $AUTOLS_DIR != $PWD ]] && ls --color=always
  AUTOLS_DIR=$PWD
}

function gor {
  while [ ! -f Gemfile ] && [[ $PWD != '/' ]]; do
    cd ..
  done
}

function ch {
	if [ $# -eq 0 ]; then
		cd $HOME
	else
		cd $HOME/$1
	fi
}

function mcdir {
  mkdir $@
  cd "${@:$#}"
}

function ermdir {
  if [[ $# -ne 1 ]]; then
    echo "usage: ermdir DIR    erase empty directories" >&2
    return 1
  fi

  rmdir -p $1/**/ 2> /dev/null
}

function sizes {
  du -sm ./$1/* | sort -nr
}

function binfo {
  where $1
  $1 --version
}

function help {
  $1 --help 2>&1 | less -SRn
}

#color {{{
function colors {
  local c
  echo
  for c in {000..255}; do
    echo -n "\e[38;5;${c}m $c"
    [ $(($c%16)) -eq 15 ] && echo
  done
  echo
}

function colorsv {
  local c
  echo
  for c in {016..255}; do
    echo -n "\e[38;5;${c}m $c"
    [ $(($((c-16))%6)) -eq 5 ] && echo
  done
  echo
}
# }}}

#link# {{{
function sln {
  local sour targ
  if [[ $# < 2 ]]; then
    echo "usage: sln <source> <target>"
    return 1
  else
    sour=${1:a}
    targ=${2:a}
    ln -svi $sour $targ
  fi
}

function binln {
  for name in $@; do
    ln -svi ${name:a} $HOME/bin/${name:t:r}
  done
}
# }}}

#rc commands {{{
function generic_rc {
  set -u
  : $1
  local dir="$1"
  shift
  case $# in
    0) vi $dir ;;
    *)
      case $1 in
        '-d' | '--dir' )  echo $dir ;;
        '-l' | '--list' ) ls $dir ;;
        '-c' | '--cd' )   cd $dir ;;
        '-g' | '--git' )
          shift
          if [[ $# -eq 0 ]]; then
            git -C $dir sh
          else
            git -C $dir $@
          fi
          ;;
        * )
          if [ -e $dir/$1 ]; then
            vim $dir/$1
          else
            echo "$1 is not exists" >&2
          fi
      esac
      ;;
  esac
}

function vimrc {
  generic_rc $HOME/.vim/ $@
}

function zshrc {
  generic_rc $HOME/.dotfiles/zsh/ $@
}
# }}}

function dotgit { git -C ~/.dotfiles ${1:-sh} }
function vimgit { git -C ~/.vim ${1:-sh} }
function dcho   { eval "echo \$$1" }
function edigo  { vi $1 && $2 $1 }

function +r { chmod +r $1 }
function -r { chmod -r $1 }
function +w { chmod +w $1 }
function -w { chmod -w $1 }
function +x { chmod +x $1 }
function -x { chmod -x $1 }

function pull-these {
  for i in `ls -F | grep '/$'`; do
    printf "%-20s" $i
    git -C $i pull
  done
}

function cons-web {
  if [[ $# -ne 3 ]]; then
    echo "usage: cons-web <url> <start-num> <last-num>"
    echo "       open the consecutive web pages"
    return 0
  fi

  for i in {$2..$3}; do
    opera-beta $1"$i"
  done
}

function host-list {
  sudo nmap -sP 192.168.1.1/24 | sed -e "s/^\(MAC.*\)$/\1 \n/"
}

function samp { # {{{
  if [[ $#  -eq 0 ]]; then
    echo "usage: samp TYPE" >&2
    return 1
  fi

  local type file
  type=$1

  # ファイルとパスを設定
  if [[ $# -lt 2 ]]; then
    file=$(mktemp  --suffix=.${type})
  else
    echo "$2"
    file="$PWD/$2.$type"

    if [[ -e $file ]]; then
      echo "$file already"
      return 1
    fi
  fi

  cat $HOME/Templates/pg/${type}.${type} > $file

  if [[ $? != 0 ]]; then
    echo "Error: $file is not writable"
    return 1
  fi

  vim + $file
} # }}}

function dump {
  if [[ $# -eq 0 ]]; then
    echo "usage: dump BIN [SYMBOL] [OUTPUT_FILE]" >&2
    return 1
  fi
  objdump -M intel -S "$1" | block "<.?${2:-main}.:" "^\n" | sed "/^$/d" | tee ${3:-${1%%.*}}.disa
}

# 再帰的にディレクトリのみパーミッションを変更する
function dhmod {
  ls -F | grep "/" | xargs chmod -R $1
}

#rubyとかで作りなおしたほうがいいかも
#マッチした部分のon/off
#特定ディレクトリの無視、検索条件指定
#errorの表示 on/off
function findstr { # {{{
  if [ $# -lt 1 ];then
    echo "findstr [DIR] [OPTS] STR" 1>&2
    return 1
  fi

  local location str opt

  if [[ $# == 1 ]];then
    location="."
  else
    location=$1
    shift
  fi

  while [ $# -gt 1 ]; do
    opt="$opt $1"
    shift
  done

  str=$1

  # echo "location: $location"
  # echo "opt: $opt"
  # echo "str: $str"

  find $location $opt -type f -exec grep -IHl "$str" {} \; 2> /dev/null
} # }}}

#udd # {{{
function udd-add {
  [[ $USER == 'root' || $PWD == $HOME ]] && return
  cdd add "t$(gettty)"
}

function udd-delete {
  [[ $USER == 'root' ]] && return
  cdd delete "t$(gettty)" 2>/dev/null
}

function udd {
  if [[ $# == 0 ]]; then
    cat ~/.cdd
  else
    cdd "t$1"
  fi
}
# }}}

#percol
pc(){
  if [[ $# == 0 ]]; then
    echo "usage: pc [mode [target]] [command]"
    return 1
  fi

  case $1 in
    '-h'|'--history')
      cmd="history -nr -${2:-1000}" ;;
    '-f'|'--file')
      cmd="find ${2:-.} -type f" ;;
    '-d'|'--directory')
      cmd="find ${2:-.} -type d" ;;
    '-a'|'--all')
      cmd="find ${2:-.}" ;;
    *)
      cmd=$* ;;
  esac

  result=$(eval $cmd 2>/dev/null | percol)

  case $1 in
    '-h'|'--history')
      eval $result ;;
    '-f'|'--file')
      cd ${result:h} ;;
    '-d'|'--directory')
      cd ${result} ;;
    '-a'|'--all')
      if [[ -f $result ]]; then
        cd ${result:h}
      else
        cd ${result}
      fi
      ;;
    *)
      print -z $* ;;
  esac
}

pd(){ pc -d ${1:-} }
pf(){ pc -f ${1:-} }
ph(){ pc -h ${1:-} }

google_translate() {
  local arg en_jp opt

  arg=`echo ${@} | sed -e 's/  */+/g'`
  en_jp="?hl=ja&sl=en&tl=ja&ie=UTF-8&oe=UTF-8" # url
  opt="${en_jp}&text=${arg}"
  w3m "http://translate.google.com/${opt}" | block '\[\s+\]' '\[\s+\]' | tail --lines=+2
}
