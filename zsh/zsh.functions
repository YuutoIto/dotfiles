#!/usr/bin/env zsh -u

# TODO PATH通せばよくね
#      ヘルパー関数への依存がある
autoload -Uz ruby-calc
autoload -Uz enc-utf8
autoload -Uz enc264
autoload -Uz clip
autoload -Uz sticky-enable-gnome
autoload -Uz generic_rc
autoload -Uz cons-web
autoload -Uz pull-these
autoload -Uz countdown
autoload -Uz colorso
autoload -Uz colorsv
autoload -Uz google_translate
autoload -Uz google
autoload -Uz rmhere
autoload -Uz sym_solve
autoload -Uz filter_select_file
autoload -Uz proxy-on
autoload -Uz proxy-off
autoload -Uz dein
autoload -Uz cargo-cd

is_num(){ expr "${1}" + 1 >&! /dev/null }
is_elf(){ file ${1} | awk '$0=$2' | grep -q ELF }
is_active(){ systemctl status "${1:?service}" | grep -Po '(?<=Active:).*' | grep -wq 'active' }
in_group(){ groups "${USER}" | grep -q "${1:?group name}" }

rand() { ruby -e "puts Random.new.rand(${1:? range})" }
getuuid(){ ruby -r securerandom -e 'print SecureRandom.uuid' }
gettty(){ echo "$TTY" | sed -e 's/[^0-9]//g' }
relative(){ echo "${1:a}" | sed -e "s|^${PWD}/\?||" }
warn(){ echo $* >&2 }
back(){ eval $@ >&! /dev/null <&- &! } # nohup?
hide(){
  out=$(mktemp --suffix=".$(date +%T).${1}.out")
  err=$(mktemp --suffix=".$(date +%T).${1}.err")

  { eval $@ > "${out}"  2> "${err}" <&-
    cat "${out}"
    cat "${err}" >&2
  } &!
}

line(){
  h="${1:?head}"
  t="${2:?tail}"
  cat - | head "-${h}" | tail "-${t}"
}

echo-color(){
  color="${1}"
  shift
  echo -e "\e[38;5;${color}m${*}\e[00m"
}

echo-true(){
  red="${1}"
  green="${2}"
  blue="${3}"
  shift 3
  echo -e "\x1b[38;2;${red};${green};${blue}m${*}\x1b[0m"
}

autols(){
  [[ ${AUTOLS_DIR:-$PWD} != $PWD ]] && ls -F --color=always -v
  AUTOLS_DIR="${PWD}"
}
add-zsh-hook precmd autols

ch(){ cd $HOME/${1:-} }

# for completion
startvm(){
  back VBoxManage startvm ${@}
}

mkd(){
  mkdir -p $@
  cd "${@:$#}"
}

rmd(){
  if [[ $# -eq 0 ]]; then
    warn "usage: ermdir <DIR> erase empty directories"
    return 1
  fi

  find ${@} -type d -empty | xargs -I{} rmdir --ignore-fail-on-non-empty -p "{}"
}

hat(){
  for i in ${@}; do
    pygmentize -O style=native -f terminal256 -g "${i}" 2>/dev/null || command cat "${i}"
  done
}

cats(){
  for i in ${@}; do
    echo "---- ${i}"
    hat "${i}"
    echo
  done
}

xopen(){
  for i in ${@:-.}; do
    if [[ ! -e ${i:-.} ]]; then
      warn "${i:-.} is not exists"
      return 1
    fi
    xdg-open ${i:-.} >/dev/null 2>&1
  done
}

vboxresize(){ VBoxManage modifymedium --resize $((1024*$1)) $2 }
title()  { echo -ne "\033]0;$1\007" }
binfo()  { which -aps $1 && $1 --version }
help()   { $1 --help 2>&1 | less }
edigo()  { $EDITOR "$1" && $2 "$1" }
lls()    { ls -lgG --color=always  ${@-} | awk '{print $7, $8, $9}' }
dotgit() { git -C $DOT_DIR ${@:-sh} }
vimgit() { git -C ~/.vim ${@:-sh} }
md5text(){ echo -n "${1}" | md5sum | cut -f1 | cut -d \  -f 1 }
youtube-mp3() { youtube-dl -x --audio-format mp3 "${@:-$(clip -o)}" }
youtube-ogg() { youtube-dl -x --audio-format vorbis "${@:-$(clip -o)}" }
setuid(){ sudo chown root: "${1}" && sudo chmod u+s "${1}" }
mve(){ mv "${1}" "${1:r}.${2}" }

+r(){ chmod u+r $@ }
-r(){ chmod a-r $@ }
+w(){ chmod u+w $@ }
-w(){ chmod a-w $@ }
+x(){ chmod u+x $@ }
-x(){ chmod a-x $@ }

vimrc(){ generic_rc $HOME/.vim/ $@ }
zshrc(){ generic_rc $ZSH_DOT_DIR $@ }

sgrep(){
  setopt dotglob # Need localloopts
  grep --color=auto --line-number ${@} **/* 2>/dev/null
}

git-set-pushurl(){
  git config remote.origin.pushurl "$(git config remote.origin.url)"
  git config remote.origin.url "$(git config remote.origin.url | tr ':' '/' | sed -e 's|^git@|https://|')"
}

git-hash(){
  branch="$(git branch | peco --select-1 | awk '$0=$NF')"
  [[ -z $branch ]] && return
  git log --oneline "${branch}" | peco | awk '$0=$1'
}

hosts(){
  nmap -sP "${1:-$(ip-sub | head -1)}" | sed -e "s/^\(MAC.*\)$/\1\n/"
}

dump(){
  if [[ $# -eq 0 ]]; then
    echo "usage: dump BIN [SYMBOL] [OUTPUT_FILE]" >&2
    return 1
  fi
  objdump -M intel -S "$1" | block "<.?${2:-main}.:" "^\n" | sed "/^$/d" | tee ${3:-${1%%.*}}.disa
}

# 再帰的にディレクトリのみパーミッションを変更する
dhmod(){
  ls -F | grep "/" | xargs chmod -R $@
}

#ud # {{{
ud(){
  case "${1:-}" in
    list)
      grep '^\.' ~/.cdd | sort -u -t: -k 2,2 | sed -e 's/^\.//' | sed -e 's/:/: /' | sort -n
      ;;
    '')
      dir="$(ud list | peco)"
      [[ -n $dir ]] && ud "${dir}"
      ;;
    *)
      cdd ".${1:?}"
      ;;
  esac
}

ud-add(){
  if can_exec cdd && [[ $UID -ne 0 && $PWD != $HOME ]]; then
    cdd add ".$(gettty)"
  fi
}
add-zsh-hook precmd ud-add
# }}}

# peco cd # {{{
pc(){
  if [[ $# -eq 0 ]]; then
    warn "usage: pc command"
    return 1
  fi
  print -z $(eval $* 2>/dev/null | peco)
}

pa(){
  result=$(find ${@:-.} 2>/dev/null | peco)
  [[ -z $result ]] && return
  if [[ -d $result ]]; then
    cd "${result}"
  else
    cd "${result:h}"
  fi
}

pd(){ pa "$1" -type d }
# cd to file dir
pf(){ pa "$1" -type f }
# }}}

# improved tree
uree(){
  local s
  local -a opt
  for s in $@; do
    is_num "${s}" && opt+='-L'
    opt+="${s}"
  done
  command tree -C -l ${opt} | less -i --RAW-CONTROL-CHARS --LONG-PROMPT --chop-long-lines --quit-if-one-screen --no-init
}

sizes(){
  dir="${1:+$(relative $1)/}"
  ls -A "${dir:-./}" | sed "s|^|${dir}|g" | xargs -I{} du -sm '{}' 2>/dev/null | sort -n
}

lsize(){
  du -sh -- "${*:+$(relative $*)/}"*
}

# pgrep(){
#   command pgrep "${*}" | xargs --no-run-if-empty ps -p
# }

kill(){
  if (($# != 0)); then
    command kill --verbose $@
  else
    ps --no-headers -u "${USER}" -o pid,cmd | peco | awk '$0=$1' | xargs kill --verbose
  fi
}

mac-vendor(){
  if (($# != 0)); then
    curl -sS http://api.macvendors.com/$(echo "${1:?Need mac address}" | sed -s 's/..:..:..$/00:00:00/g')
    echo
  else
    while read line; do
      mac-vendor "${line:?Need mac address}"
    done
  fi
}

mac-arp(){
  arp -n | awk '/:/{printf("%-15s %s\n", $1, $3)}' | while read line
  do
    echo -n "${line}   "
    mac-vendor "$(echo "${line}" | awk '$0=$2')"
  done
}

google-asm(){
  back google "${1:?opecode}" site:softwaretechnique.jp
}

gdb-disas(){
  gdb -q --nx --batch --init-eval-command='set disassembly-flavor intel' --eval-command="disas ${2:-main}" "${1:-a.out}"
}

gots(){
  readelf -r "${1:?ELF binary}" | egrep '^[0-9]' | sort -n | sed -e 's/@.*$//' -e 's/__\+//g' | awk '$0=$5 " = 0x" $1'
}

# trash # {{{

# Verify write permission.
# Ignore directory.
trash-safe-file(){
  if [[ -d ${name} ]]; then
    warn "cannot remove '${name}': Is a directory"
    return 0
  elif [[ -w ${1} ]]; then
    echo -n "remove write-protected file ${1}? " >&2
    if ! $(read -q); then
      return 0
    fi
  fi
  trash "${1}"
}

# ディレクトリが内包する全てのファイルの削除判定をしてまとめて消す
trash-safe-dir(){
  echo-color 31 'trash: write protected files'
  find "${1}" -type f -not -writable E
  echo-color 31 'trash: write protected dirs'
  find "${1}" -type d -not -readable -or -type d -not -writable -or -type d -not -executable E
  echo

  if $(read -q "?Remove ${1} including these files? "); then
    trash "${1}"
    echo -e '\nRemoved!'
  else
    echo
  fi
}

trash-elf(){
  for i (*); do
    is_elf "${i}" &&  trash "${i}"
  done
}
# }}}

mirrorsort(){
  sudo cp /etc/pacman.d/mirrorlist /etc/pacman.d/mirrorlist.cp
  sudo rankmirrors -m rank
}

lsmod(){
  if (($# == 0)); then
    command lsmod
  else
    command lsmod | grep -E "${*}"
  fi
}

sl(){
  sleep 0.4
  echo 'l「もしかして」'
  sleep 1.2
  echo 's「私たち」'
  sleep 1.4
  echo '「「入れ替わってる！？！？！？」」'
}

test-build(){
  : "${1:? source file}"
  "${CMD}" -O0 -m32 -fno-stack-protector -o "${1%.*}" "${1}"
}

gccs(){
  export CMD=gcc
  test-build ${@}
}

clangs(){
  export CMD=clang
  test-build ${@}
}

elf-sec(){
  objdump -d -j ."${2:-init}" --no "${1:-a.out}"
}

fork-server(){
  # 引数の順序依存
  socat "TCP-LISTEN:${2:?port},reuseaddr,fork" "EXEC:${${1:a}:?file}" &
}

doko(){
  find ${2:-} -iname "*${1}*"
}

aslr(){
  sudo sysctl -w kernel.randomize_va_space=${1:-2}
}

ptrace(){
  sudo sysctl -w kernel.yama.ptrace_scope=${1:-2}
}

exzip(){
  for i in ${@}; do
    unzip -d "${i:r}" "${i}"
  done
}

wgetc(){
  clip -o
  echo
  if (($# == 0)); then
    wget "$(clip -o)"
  else
    wget -O "${1:?filename}" "$(clip -o)"
  fi
}

gif-optimize(){
  convert "${1:?gif path}" -fuzz 7% -layers Optimize "${1:r}.opt.gif"
}

json_format(){
  tmpfile=`mktemp`
  cat "${1:?JSON file}" | jq . > "${tmpfile}"
  command mv "${tmpfile}" "${1}"
}


# TODO
tops(){
  return

  top
  htop
  nethogs
  iftop
  iotop
  atop      アクティブなプロセスのみ?
  slabtop   メモリ
  jnettop   ホストとポートごとに表示
}

stats(){ # TODO
  return

  # https://blog.riywo.com/2011/02/20/192123/
  ifstat
  netstat -taopn
  vmstat
  iostat -x
  dstat -f
  mpstat
}

smart_docker(){
  unfunction "${1}"

  if ! is_active docker.service; then
    echo 'Start docker'
    sudo systemctl start docker.service
  fi

  if ! in_group docker; then
    alias "${1}"="sudo ${1}"
  fi
}

docker(){
  smart_docker docker
  docker ${@}
}

docker-compose(){
  smart_docker docker-compose
  docker-compose ${@}
}

docker-images-update(){
  sudo docker images | awk 'NR>1 && $0=$1' | sort -u | egrep -v '^(<none>)$' | xargs -P0 -L1 -I@ sudo docker pull @
}

sec(){
  : "${1:?binary file}"

  file "${1}" | tr ',' '\n'
  echo
  checksec -f "${1}" | awk 'NR==2{$20="";print}'
  echo
  readelf -h "${1}"
  readelf -r "${1}"
}

ps_get(){
  ps --no-headers -o "${1:?format}" -p "${2:?pid}"
}

ps_stime(){
  ps_get stime "${1:?pid}"
}

ps_time(){
  ps_get time "${1:?pid}"
}

winfocus(){
  : "${1:?app name}"
  eval "$(xdotool search --shell --onlyvisible --class "${1}")"

  if (($#WINDOWS == 0)); then
    warn 'Not found'
    return 1
  fi

  # only older
  win_num="$(for i in ${WINDOWS}; do
    ps_time "$(xdotool getwindowpid "${i}")" | tr '\n' ' '
    echo "${i}"
  done | sort -nr | head -1 | awk '$0=$2')"

  xdotool windowactivate "${win_num}"
}

typeset -g -a my_precmd_funcs
tmp_precmd(){
  if (($# == 0)); then
    echo "${my_precmd_funcs}" | tr ' ' '\n'
    return 0
  fi

  if [[ "${1}" == '-d' ]]; then
    shift
    # TODO
    return 0
  fi

  my_precmd_funcs+="${1}"
}

exec_my_precmd(){
  for cmd in ${my_precmd_funcs}; do
    eval "${cmd}"
  done
}
add-zsh-hook precmd exec_my_precmd

list_dirs_files(){
  dir="${1:-.}"
  dir="${dir:a}/"
  shift
  find "${dir}" $@ -type f 2>/dev/null | sed -e  "s|^${dir}||"
}

vim-version(){ # {{{
  install_dir="${HOME}/works/vim-versions"
  if (($# == 0)); then
    ls "${install_dir}"
    return 0
  fi
  version="${1}"
  dir="${install_dir}/${version}"

  if ! [[ -d $dir ]]; then
    cd ~/src/vim
    gch "v${version}"
    make distclean >&! /dev/null
    make "-j $(nproc --ignore 1)"
    ./configure --prefix="${dir}"
    make && make install
    make distclean >&! /dev/null
    git ch -
    cd -
  fi

  export PATH="${dir}/bin:$PATH"
  echo "${dir}/bin"
} # }}}

vim-wl(){
  cat $(git ls-files | grep -Ev 'colors/|snippets/|neobundle.vim') | grep -Ev '^\s*("|$)' | wc -l
}

office2pdf(){
  libreoffice --headless --invisible --convert-to pdf ${*:?Source files}
}

show(){
  db=${1:?db}
  table=${2:?table}
  echo "select * from ${table};" | sqlite3 "${db}"
}

httpd(){
  dir="${1:-.}"
  port="${2:-4000}"

  if in_path ruby; then
    ruby -run -e httpd "${dir}" -p "${port}"
  else
    (cd ../ && python3 -m http.server "${port}")
  fi
}

get-active-connection(){
  nmcli connection show --active | grep -v 'NAME' | awk '$0=$1'
}

gitkraken(){
  back command gitkraken --path="${1:-./}"
}

click(){
  startwait="${1:?start wait}"
  timespan="${2:?time span}"
  count="${3:?click count}"

  sleep "${startwait}"
  echo 'click start'

  for ((i=0; i < ${count}; i++)) ; do
    sleep "${timespan}"
    xdotool click 1
  done
}

add() {
  file="${1:?destination path}"
  str="${2:?append string}"
  echo "${str}" >> "${file}"
}

comet(){
  f="${1:?casl file}"
  pycasl2 "${f}"
  pycomet2 "${f:r}.com" -d -c
}

timer(){
  ruby -e "
    loop do
      time = Time.now.strftime('%T.%1L')
      print(\"\\r\\e[0J#{time}\")
      # \e[H\e[2J

      if /${1:-^}$/ =~ time
        system('xdotool mousedown 1')
        sleep(0.03)
        system('xdotool mouseup 1')
        print(\"\\r\\e[0J#{Time.now.strftime('%T.%3L')}\")
        break
      end

      sleep(0.03)
    end
    "
}

# docker-compose ictsc-score-server
idc(){
  cmd=${1:? back api}
  case "${cmd}" in
    'back')
      docker-compose up -d --no-deps redis plasma db ui
      [[ $? == 0 ]] && echo-color 45 'Start [redis plasma db ui] in background'
      ;;
    'api')
      docker-compose up api
      # sudo docker-compose up --build api
      ;;
  esac
}

unzip-recv() {
  broken_zip="${1:?broken zip}"
  tmp_zip="$(mktemp --suffix=.zip)"
  echo 'y' | zip --fixfix "${broken_zip}" --out="${tmp_zip}"
  unzip "${tmp_zip}"
}
